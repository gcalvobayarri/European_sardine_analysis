model{
  for (i in 1: N){
    for (t in 1: T){
      Y1[i, t] ~ dnorm(Mi1[i, t], lambda) 
      Mi1[i, t] <- beta01 + b0[i,1] + (b1[i,1]) * (t - 1)
      Y2[i, t] ~ dnorm(Mi2[i, t], lambda) 
      Mi2[i, t] <- beta02 + b0[i,2] + (b1[i,2]) * (t - 1)
    }
  }

  for (t in 1: T){
    # Albania
    Y1[10, t] ~ dnorm(Mi1[10, t], lambda) 
    Mi1[10, t] <- beta01 + b0Albania + (b1Albania) * (t - 1)

    # EspaÃ±a
    Y1[11, t] ~ dnorm(Mi1[11, t], lambda) 
    Mi1[11, t] <- beta01 + b0Espana + (b1Espana) * (t - 1)

    # Bosnia and Herzegovina 
    Y2[10, t] ~ dnorm(Mi2[10, t], lambda) 
    Mi2[10, t] <- beta02 + b0Bosnia + (b1Bosnia) * (t - 1)
    }


  ## PRIORS
  # lambda
  
  lambda <- 1 / (sigma * sigma)
  sigma ~ dunif(0,10)
  
  
  # Mi prior

  beta01 ~ dnorm(0, 0.01)
  beta02 ~ dnorm(0, 0.01)

  
  
  # b0, b1 prior
  for (i in 1: N){
    b0[i, 1:2] ~ dmnorm.vcov(rep(0,2), Sigma0[,])
    b1[i, 1:2] ~ dmnorm.vcov(rep(0,2), Sigma1[,])
  }

  b0Albania ~ dnorm(0, tau01)
  b0Espana ~ dnorm(0, tau01)
  b0Bosnia ~ dnorm(0, tau02)

  b1Albania ~ dnorm(0, tau11)
  b1Espana ~ dnorm(0, tau11)
  b1Bosnia ~ dnorm(0, tau12)
 
  I01_sigma ~ dunif(0,10)
  tau01 <- 1 / (I01_sigma * I01_sigma)
  
  I02_sigma ~ dunif(0,10)
  tau02 <- 1 / (I02_sigma * I02_sigma)
  
  T11_sigma ~ dunif(0,10)
  tau11 <- 1 / (T11_sigma * T11_sigma)
  
  T12_sigma ~ dunif(0,10)
  tau12 <- 1 / (T12_sigma * T12_sigma)

  nu_0 ~ dunif(-1, 1)
  nu_1 ~ dunif(-1, 1)

# Precision matrices
  Sigma0[1,1] <- I01_sigma * I01_sigma
  Sigma0[1,2] <- nu_0 * I01_sigma * I02_sigma 
  Sigma0[2,1] <- nu_0 * I01_sigma * I02_sigma 
  Sigma0[2,2] <- I02_sigma * I02_sigma

  Sigma1[1,1] <- T11_sigma * T11_sigma
  Sigma1[1,2] <- nu_1 * T11_sigma * T12_sigma 
  Sigma1[2,1] <- nu_1 * T11_sigma * T12_sigma 
  Sigma1[2,2] <- T12_sigma * T12_sigma
}